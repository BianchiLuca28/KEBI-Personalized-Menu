#Query that checks for all the preferences of the guest
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mod: <http://fhnw.ch/modelingEnvironment/ModelOntology#>
PREFIX lo: <http://fhnw.ch/modelingEnvironment/LanguageOntology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX : <http://www.semanticweb.org/utente/ontologies/2024/5/project_ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?meal
WHERE {
  {
    SELECT ?isVegetarian ?isLactoseIntolerant ?isGlutenIntolerant ?isCalorieConscious WHERE {
      mod:SuggestMeals_f8d5a1e9-c95f-4994-9f1b-e33a686311de lo:Vegetarian ?vegetarianObj .
      mod:SuggestMeals_f8d5a1e9-c95f-4994-9f1b-e33a686311de lo:Lactose_Intolerant ?lactoseObj .
      mod:SuggestMeals_f8d5a1e9-c95f-4994-9f1b-e33a686311de lo:Gluten_Intolerant ?glutenObj .
      mod:SuggestMeals_f8d5a1e9-c95f-4994-9f1b-e33a686311de lo:Calorie_Conscious ?calorieObj .
      BIND(xsd:boolean(?vegetarianObj) AS ?isVegetarian)
      BIND(xsd:boolean(?lactoseObj) AS ?isLactoseIntolerant)
      BIND(xsd:boolean(?glutenObj) AS ?isGlutenIntolerant)
      BIND(xsd:boolean(?calorieObj) AS ?isCalorieConscious)
    }
  }

  ?meal rdf:type :Meal .

  FILTER(
    (?isVegetarian = false || NOT EXISTS {
      ?usage :IngredientUsageInMeal ?meal ;
             :UsedIngredient ?ingredient .
      ?ingredient :IngredientNotCompatibleWithFoodTypePreference :Vegetarian .
    }) &&
    (?isLactoseIntolerant = false || NOT EXISTS {
      ?usage :IngredientUsageInMeal ?meal ;
             :UsedIngredient ?ingredient .
      ?ingredient :IngredientNotCompatibleWithFoodTypePreference :Lactose_Intolerant .
    }) &&
    (?isGlutenIntolerant = false || NOT EXISTS {
      ?usage :IngredientUsageInMeal ?meal ;
             :UsedIngredient ?ingredient .
      ?ingredient :IngredientNotCompatibleWithFoodTypePreference :Gluten_Intolerant .
    })
  )

  FILTER(
    (?isCalorieConscious = false || EXISTS {
      SELECT ?meal (SUM(?calories) AS ?totalCalories)
      WHERE {
        ?usage :IngredientUsageInMeal ?meal ;
                :UsedIngredient ?ingredient ;
                :IngredientUsageInGrams ?quantity .
        
        ?ingredient :IngredientHasCalories ?caloriesPer100Grams .
        
        BIND((?caloriesPer100Grams / 100.0) * ?quantity AS ?calories)
      }
      GROUP BY ?meal
      HAVING (SUM(?calories) <= 550)
    })
  )
}
